buildscript {
    ext {
        springBootVersion = '2.2.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.2.1'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE'
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'jacoco'

group = 'com.raksit.example'
version = '0.0.1'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.cloud:spring-cloud-starter-kubernetes-config:1.1.1.RELEASE'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'org.flywaydb:flyway-core'
    compile 'org.projectlombok:lombok:1.18.4'
    runtime 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testCompile 'org.mockito:mockito-core:3.2.4'
    testCompile 'org.mockito:mockito-junit-jupiter:2.19.1'
    testCompile 'com.github.javafaker:javafaker:0.16'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testCompile 'io.rest-assured:rest-assured:4.2.0'
    testCompile 'io.zonky.test:embedded-database-spring-test:1.5.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

dependencyManagement {
    imports {
        mavenBom 'io.zonky.test.postgres:embedded-postgres-binaries-bom:12.1.0'
    }
}

testSets {
    integrationTest {
        dirName = 'integration-test'
    }
}

test {
    testLogging.showStandardStreams = false
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

integrationTest {
    testLogging.showStandardStreams = false
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

bootRun {
    if (System.properties.containsKey('spring.profiles.active')) {
        systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    } else {
        systemProperty 'spring.profiles.active', 'local'
    }
}

checkstyle {
    toolVersion = "8.10.1"
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    maxErrors = 0
    maxWarnings = 0
    reportsDir = file("$project.buildDir/reports/checkstyle")
}

pmd {
    toolVersion = "6.21.0"
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = ["java-basic", "java-braces"]
}

jacoco {
    toolVersion = '0.8.2'
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination = file("$buildDir/reports/jacoco")
    }
}

check.dependsOn(integrationTest)
test.shouldRunAfter(pmdMain)
integrationTest.shouldRunAfter(test)


